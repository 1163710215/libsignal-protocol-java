buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath files('libs/gradle-witness.jar')
    }
}

apply plugin: 'com.android.library'
apply plugin: 'witness'
apply plugin: 'maven'
apply plugin: 'signing'

version = "1.0.0"
group   = "org.whispersystems"

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:2.5.0'
}

dependencyVerification {
    verify = [
      'com.google.protobuf:protobuf-java:e0c1c64575c005601725e7c6a02cebf9e1285e888f756b2a1d73ffa8d725cc74',
    ]
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    android {
        sourceSets {
            main {
                jniLibs.srcDirs = ['libs']
            }
        }
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: whisperSonatypeUsername, password: whisperSonatypePassword)
        }

        pom.project {
            name 'axolotl-android'
            packaging 'aar'
            description 'Axolotl encryption library for Android'
            url 'https://github.com/WhisperSystems/libaxolotl-android'

            scm {
                url 'scm:git@github.com:WhisperSystems/libaxolotl-android.git'
                connection 'scm:git@github.com:WhisperSystems/libaxolotl-android.git'
                developerConnection 'scm:git@github.com:WhisperSystems/libaxolotl-android.git'
            }

            licenses {
                license {
                    name 'GPLv3'
                    url 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    name 'Moxie Marlinspike'
                }
            }
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}
